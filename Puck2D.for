! WRITTEN BY VINEETH RAVI
! FOLLOWING IS A UMAT CODE FOR LAMINA
! VARIABLES FROM THE LETTER I TO N ARE CONSIDEREDED TO BE INTEGERS
! AND VARIABLES STARTING WITH OTHER LETTERS ARE DOUBLE PRECISION
! SO PLEASE BE CAREFUL IN NAMING THE VARIABLES
! EXAMPLES OF FEW DEFAULT INTEGERS : NTENS, NDI, NSHR
! EXAMPLES OF FEW DEFAULT REAL NUMBERS : DDSDDE, STRESS, STRAN
! THE KEYWORD *Transverse Shear Stiffness NEEDS TO ENTERED BEFORE CARRYING OUT THE SIMULATIONS
! MORE DETAILS CAN BE FOUND IN THE ABAQUS MANUAL
      
      SUBROUTINE UMAT(STRESS,STATEV,DDSDDE,SSE,SPD,SCD,
     1 RPL,DDSDDT,DRPLDE,DRPLDT,
     2 STRAN,DSTRAN,TIME,DTIME,TEMP,DTEMP,PREDEF,DPRED,CMNAME,
     3 NDI,NSHR,NTENS,NSTATV,PROPS,NPROPS,COORDS,DROT,PNEWDT,
     4 CELENT,DFGRD0,DFGRD1,NOEL,NPT,LAYER,KSPT,JSTEP,KINC)

      INCLUDE 'ABA_PARAM.INC'

      CHARACTER*80 CMNAME
      DIMENSION STRESS(NTENS),STATEV(NSTATV),
     1 DDSDDE(NTENS,NTENS),DDSDDT(NTENS),DRPLDE(NTENS),
     2 STRAN(NTENS),DSTRAN(NTENS),TIME(2),PREDEF(1),DPRED(1),
     3 PROPS(NPROPS),COORDS(3),DROT(3,3),DFGRD0(3,3),DFGRD1(3,3),
     4 JSTEP(4)
      
      PARAMETER (ZERO = 0.0D0, ONE = 1.0D0, TWO = 2.0D0)
	 
      E11 = PROPS(1)     ! ELASTIC MODULUS IN THE LONGITUDINAL 1 DIRECTION
      E22 = PROPS(2)     ! ELASTIC MODULUS IN THE TRANSVERSE 2 DIRECTION
      PR12 = PROPS(3)    ! MAJOR POISSONS RATIO IN 1-2 PLANE
      G12 = PROPS(4)     ! SHEAR MODULUS IN 1-2 PLANE
      
      XT = PROPS(5)      ! LONGITUDINAL STRENGTH IN THE 1 DIRECTION
      XC = PROPS(6)      ! COMPRESSIVE STRENGTH IN THE 1 DIRECTION
      YT = PROPS(7)      ! LONGITUDINAL STRENGTH IN THE 2 DIRECTION
      YC = PROPS(8)      ! COMPRESSIVE STRENGTH IN THE 2 DIRECTION
      S = PROPS(9)       ! SHEAR STRENGTH IN THE 1-2 PLANE
      A0 = PROPS(10) ! 
      XLAMBDA_MIN = PROPS(11)
      IFLAG = PROPS(12)     ! FLAG FOR INDICATING THE TYPE OF MATERIAL. FOR CFRP IFLAG = 1. FOR GFRP IFLAG = 2 

      PR21 = (E22*PR12)/E11 ! CALCULAION OF MINOR POISSONS RATIO	   
		   
      DO I=1,NTENS        
          DO J=1,NTENS
              DDSDDE(I,J) = ZERO     ! INITIALIZATION OF JACOBIAN MATRIX
          ENDDO
      ENDDO
	   
      DDSDDE(1,1) = E11/(1-PR12*PR21)
      DDSDDE(1,2) = PR12*E22/(1-PR12*PR21)
      DDSDDE(2,1) = PR12*E22/(1-PR12*PR21)
      DDSDDE(2,2) = E22/(1-PR12*PR21)
      DDSDDE(3,3) = G12
      
      DO I=1, NTENS             
          DO J=1, NTENS
              STRESS(I) = STRESS(I) + DDSDDE(I, J)*DSTRAN(J)     ! UPDATING STRESS TENSOR  
          END DO
      END DO
      
      IF ( IFLAG .EQ. 1 ) THEN
          PT21 = 0.35
          PC21 = 0.30
      ELSE IF ( IFLAG .EQ. 2 ) THEN
          PT21 = 0.3
          PC21 = 0.25
      END IF
      
      STATEV = ZERO
      S11=STRESS(1)     ! NORMAL STRESS ALONG 1 DIRECTION
      S22=STRESS(2)     ! NORMAL STRESS ALONG 2 DIRECTION 
      S12=STRESS(3)     ! SHEAR STRESS IN 1-2 PLANE 
      
      RA22 = (S/(TWO*PC21)) * (SQRT(ONE + TWO*PC21*YC/S) - ONE)
      PC22 = PC21*RA22/S
      S12C = S*SQRT(ONE + TWO*PC22)
      
      IF ( S11 .GT. 0.0 ) THEN     ! FIBER FAILURE IN TENSION
          STATEV(1) = S11/XT        
          AFB = STATEV(1)
      ELSE
          STATEV(2) = -S11/XC      ! FIBER FAILURE IN COMPRESSION
          AFB = STATEV(2)
      END IF
      
      IF ( S22 .GT. 0.0 ) THEN
          AZFB = (PT21*S22/S) +  SQRT((S12/S)**2 + (ONE - ( PT21*YT/S))**2 *(S22/YT)**2)     ! INTER FIBER FAILURE : MODE A
          C = AZFB/AFB
          IF ( (M .LE. C) .AND.  (C .LE. ONE/A0) ) THEN
              A = (ONE - A0)/SQRT(ONE - XLAMBDA_MIN**2)
              XLAMBDA = C*(A0 + A*SQRT(ONE + C**2*(A**2 - A0**2)))/(ONE + (C*A)**2)
              STATEV(3) = AZFB/XLAMBDA
          ELSE
              STATEV(3) = AZFB
          END IF
          
      ELSE IF  ( (ZERO .LE. ABS(S22/S12)) .AND.  (ABS(S22/S12) .LE. RA22/ABS(S12C)) ) THEN
          AZFB = (SQRT(S12**2 + (PC21*S22)**2) + PC21*S22)/S                                 ! INTER FIBER FAILURE : MODE B
          C = AZFB/AFB
          IF ( (M .LE. C) .AND.  (C .LE. ONE/A0) ) THEN
              A = (ONE - A0)/SQRT(ONE - XLAMBDA_MIN**2)
              XLAMBDA = C*(A0 + A*SQRT(ONE + C**2*(A**2 - A0**2)))/(ONE + (C*A)**2)
              STATEV(4) = AZFB/XLAMBDA
          ELSE
              STATEV(4) = AZFB
          END IF
      
      ELSE IF  (  (ZERO .LE. ABS(S12/S22)) .AND. ( ABS(S12/S22) .LE. (ABS(S12C)/RA22)) ) THEN
          AZFB = (YC/-S22)*((S22/YC)**2  + (S12/(TWO*S*(ONE + PC22)))**2)                    ! INTER FIBER FAILURE : MODE C
          C = AZFB/AFB
          IF ( (XLAMBDA_MIN .LE. C) .AND.  (C .LE. ONE/A0) ) THEN
              A = (ONE - A0)/SQRT(ONE - XLAMBDA_MIN**2)
              XLAMBDA = C*(A0 + A*SQRT(ONE + C**2*(A**2 - A0**2)))/(ONE + (C*A)**2)
              STATEV(5) = AZFB/XLAMBDA
          ELSE
              STATEV(5) = AZFB
          END IF
      END IF
 
      RETURN
      END
